WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ "''" ~ (!"''" ~ ANY)* ~ "''" }

number = @{ ASCII_OCT_DIGIT+ }
ident = @{ ('a'..'z' | "ì" | "ä" | "\'")+ }
fun_ident = @{ ident ~ ("." ~ ident)? }

expr = {
	"ke"? ~ (
        fun_call
        | number
        | ident
    )
}

fun_call = {
	ident ~ (("fa" ~ expr ~ ("sì" ~ expr)*) | "si")
}

fun_dec = {
    "lì'ukìng" ~ fun_ident ~ ("fa" ~ ident ~ ("sì" ~ ident)*)? ~ ("alu" ~ block_body ~ ( &"." | "txew"))?
}

var_dec = {
	"'u" ~ ident ~ "alu" ~ expr
}

dec = _{
	"ngop" ~ (fun_dec | var_dec)
}

if_block = {
	"txo" ~ expr ~ "," ~ "tsakrr" ~ block_body ~ (("," ~ "txokefyaw" ~ block_body) | ( &"." | "txew"))
}

loop_block = {
	"leyn" ~ ("vaykrr" ~ expr)? ~ "kem si" ~ block_body ~ "ftang"
}

statement = {
	dec | if_block | loop_block | expr
}

block_body = { statement ~ ("ulte" ~ statement)*}

program = _{ SOI ~ (statement ~ ".")* ~ EOI }
