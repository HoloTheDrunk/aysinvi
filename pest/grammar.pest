WHITESPACE = _{ " " | "\t" | NEWLINE }

id = @{ ('a'..'z' | "ä" | "ì" | "'")+ }

number = { ASCII_OCT_DIGIT+ }

inner = @{ (!(WHITESPACE ~ "sìk") ~ ANY)* }
string = ${ ("san" ~ WHITESPACE) ~ inner ~ (WHITESPACE ~ "sìk") }

var_dec = {
    "ngop" ~ (singular_var_dec | dual_var_dec | trial_var_dec | plural_var_dec)
}

singular_var_dec = {
    "'u" ~ id ~ "alu" ~ exp
}
dual_var_dec = {
    "meu" ~ id ~ "sì" ~ id ~ "alu" ~ exp ~ "sì" ~ exp
}
trial_var_dec = {
    "pxeu" ~ id ~ ("sì" ~ id){2} ~ "alu" ~ exp ~ ("sì" ~ exp){2}
}
plural_var_dec = {
    "ayu" ~ id ~ ("sì" ~ id){3,} ~ "alu" ~ exp ~ ("sì" ~ exp){3,}
}

statement = {
    var_dec ~ "."
}

comparison_operator = {
    "teng"
}

comparison = {
    id ~ "sì" ~ id ~ "livu" ~ comparison_operator
}

exp = _{
    comparison
    | number
    | string
}

program = { SOI ~ (statement | exp)* ~ EOI }
